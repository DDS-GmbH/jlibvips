plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'groovy'
    id "net.java.openjdk.shinyafox.jshell.gradle.plugin" version "1.0.5"
    id 'base'
}

group = 'io.github.codecitizen'
version = '1.4.0-beta.1'

base {
    archivesName = 'jlibvips'
}

compileTestGroovy {
    sourceCompatibility = 1.10
    targetCompatibility = 1.10
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.1.0'
    implementation 'org.codehaus.groovy:groovy:2.5.23'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}


//shouldn't this be done automatically by the shadow plugin?
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    exclude 'org/jlibvips/jna/**'
    exclude 'org/jlibvips/util/**'
}

wrapper {
    gradleVersion = '5.0'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java

            // Add Javadoc and Sources jars
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'jlibvips'
                packaging = 'jar'
                description = 'A Java interface to llibvips, the fast image processing library with low memory needs.'
                url = 'https://github.com/codecitizen/jlibvips'

                scm {
                    connection = 'https://github.com/codecitizen/jlibvips.git'
                    developerConnection = 'https://github.com:codecitizen/jlibvips.git'
                    url = 'https://github.com/codecitizen/jlibvips'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'codecitizen'
                        name = 'Alexander Partsch'
                        email = 'alexander@partsch.ninja'
                    }
                }
            }
        }
    }

    repositories {
        if (!project.hasProperty('ossrhUsername') || !project.hasProperty('ossrhPassword')) {
            println "skip uploadArchives"
            return
        }
        maven {
            def releasesRepoUrl =  'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl ='https://oss.sonatype.org/content/repositories/snapshots/'
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
